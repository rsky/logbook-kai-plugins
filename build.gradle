buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

allprojects  {
    apply plugin: 'maven-publish'
    group = 'logbook-kai-plugins'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    ext {
        vendor = '@rsky'
        license = 'MIT'
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    def lombokVersion = '1.18.30'
    def jUnitVersion = '5.10.1'
    def mockitoVersion = '2.28.2'
    def powerMockVersion = '2.0.9'

    repositories {
        mavenCentral()
    }

    dependencies {
        runtimeOnly files("$rootDir/lib/logbook-kai.jar")
        compileOnly files("$rootDir/lib/logbook-kai.jar")
        compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
        annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
        testCompileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
        testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: jUnitVersion
        testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: jUnitVersion
        testImplementation group: 'org.apiguardian', name: 'apiguardian-api', version: '1.1.2' // suppress warning on test
        testImplementation group: 'org.mockito', name: 'mockito-core', version: mockitoVersion
        testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: powerMockVersion
    }

    test {
        useJUnitPlatform()
    }

    shadowJar {
        exclude 'logbook-kai.jar'
        dependencies {
            //noinspection GroovyAssignabilityCheck
            exclude dependency('org.projectlombok:lombok')
        }
    }
}
