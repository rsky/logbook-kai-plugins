buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.1'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.1'
    id 'io.franzbecker.gradle-lombok' version '1.10'
}

allprojects  {
    apply plugin: 'maven'
    group = 'logbook-kai-plugins'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.franzbecker.gradle-lombok'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'org.junit.platform.gradle.plugin'

    ext {
        vendor = '@rsky'
        license = 'MIT'
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    def lombokVersion = '1.16.18'
    def jUnitVersion = '5.0.1'
    def mockitoVersion = '2.8.47'
    def powerMockVersion = '1.7.1'

    repositories {
        mavenCentral()
    }

    dependencies {
        runtime files("$rootDir/lib/logbook-kai.jar")
        compileOnly files("$rootDir/lib/logbook-kai.jar")
        compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: jUnitVersion
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: jUnitVersion
        testCompile group: 'org.apiguardian', name: 'apiguardian-api', version: '1.0.0' // suppress warning on test
        testCompile group: 'org.mockito', name: 'mockito-core', version: mockitoVersion
        testCompile group: 'org.powermock', name: 'powermock-api-mockito2', version: powerMockVersion
    }

    junitPlatform {
        filters {
            includeClassNamePattern '.*Test'
        }
    }

    shadowJar {
        exclude 'logbook-kai.jar'
        dependencies {
            //noinspection GroovyAssignabilityCheck
            exclude dependency('org.projectlombok:lombok')
        }
    }
}
